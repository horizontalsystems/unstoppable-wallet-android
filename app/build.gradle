plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'com.google.devtools.ksp'
    id 'org.jetbrains.kotlin.plugin.compose'
}

android {

    defaultConfig {
        applicationId "io.horizontalsystems.bankwallet"
        compileSdkVersion compile_sdk_version
        minSdkVersion min_sdk_version
        targetSdkVersion compile_sdk_version
        versionCode 153
        versionName "0.46.4"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        resourceConfigurations += ['de', 'es', 'en', 'fa', 'fr', 'ko', 'pt', 'pt-rBR', 'ru', 'tr', 'zh']

        ksp {
            arg("room.schemaLocation", "$projectDir/schemas".toString())
        }
        vectorDrawables.useSupportLibrary = true
        lintOptions {
            checkReleaseBuilds false
        }

        resValue "string", "companyWebPageLink", "https://horizontalsystems.io"
        resValue "string", "appWebPageLink", "https://unstoppable.money"
        resValue "string", "analyticsLink", "https://unstoppable.money/analytics"
        resValue "string", "appGithubLink", "https://github.com/horizontalsystems/unstoppable-wallet-android"
        resValue "string", "appTwitterLink", "https://twitter.com/UnstoppableByHS"
        resValue "string", "appTelegramLink", "https://t.me/unstoppable_announcements"
        resValue "string", "reportEmail", "support.unstoppable@protonmail.com"
        resValue "string", "releaseNotesUrl", "https://api.github.com/repos/horizontalsystems/unstoppable-wallet-android/releases/tags/"
        resValue "string", "walletConnectAppMetaDataName", "Unstoppable"
        resValue "string", "walletConnectAppMetaDataUrl", "unstoppable.money"
        resValue "string", "walletConnectAppMetaDataIcon", "https://raw.githubusercontent.com/horizontalsystems/HS-Design/master/PressKit/UW-AppIcon-on-light.png"
        resValue "string", "accountsBackupFileSalt", "unstoppable"

        buildConfigField "boolean", "FDROID_BUILD", "false"

        ndk {
            abiFilters "armeabi-v7a", "arm64-v8a"
        }
    }

    buildFeatures {
        compose true
        viewBinding true
        buildConfig true
    }

    signingConfigs {
        appCenter {
            storeFile file("./test.keystore")
            storePassword "testKeystore123"

            keyAlias "testKeystore"
            keyPassword "testKeystore123"
        }
    }

    flavorDimensions += "distribution"
    productFlavors {
        base {
            dimension = "distribution"
            signingConfig signingConfigs.debug
        }

        fdroid {
            dimension = "distribution"
            buildConfigField "boolean", "FDROID_BUILD", "true"
        }

        fdroidCi {
            dimension = "distribution"
            applicationIdSuffix ".fdroidci"
            buildConfigField "boolean", "FDROID_BUILD", "true"
            signingConfig signingConfigs.appCenter
        }

        ci {
            dimension = "distribution"

            applicationIdSuffix ".appcenter"
            versionCode System.getenv("BUILD_NUMBER")?.toInteger() ?: defaultConfig.versionCode
            signingConfig signingConfigs.appCenter
        }
    }

    buildTypes {
        debug {
            signingConfig null
            debuggable true
            minifyEnabled false
            applicationIdSuffix ".dev"
            resValue "string", "twitterBearerToken", "AAAAAAAAAAAAAAAAAAAAAJgeNwEAAAAA6xVpR6xLKTrxIA3kkSyRA92LDpA%3Da6auybDwcymUyh2BcS6zZwicUdxGtrzJC0qvOSdRwKLeqBGhwB"
            resValue "string", "uniswapGraphUrl", "https://api.thegraph.com/subgraphs/name/uniswap/uniswap-v2"
            resValue "string", "etherscanKey", "GKNHXT22ED7PRVCKZATFZQD1YI7FK9AAYE"
            resValue "string", "bscscanKey", "R396MSJNCKX2YK4EIMP3EWYAW21NSVMXRN"
            resValue "string", "otherScanKey", "FU7CYEXQEUSMXJJF8MZR6BNRMP9XT8S9CP"
            resValue "string", "is_release", "false"
            resValue "string", "guidesUrl", "https://raw.githubusercontent.com/horizontalsystems/blockchain-crypto-guides/v1.2/index.json"
            resValue "string", "eduUrl", "https://raw.githubusercontent.com/horizontalsystems/Unstoppable-Wallet-Website/refs/tags/v1.4/src/edu.json"
            resValue "string", "faqUrl", "https://raw.githubusercontent.com/horizontalsystems/Unstoppable-Wallet-Website/master/src/faq.json"
            resValue "string", "coinsJsonUrl", "https://raw.githubusercontent.com/horizontalsystems/cryptocurrencies/master/coins.json"
            resValue "string", "providerCoinsJsonUrl", "https://raw.githubusercontent.com/horizontalsystems/cryptocurrencies/master/provider.coins.json"
            resValue "string", "marketApiBaseUrl", "https://api-dev.blocksdecoded.com"
            resValue "string", "marketApiKey", "IQf1uAjkthZp1i2pYzkXFDom"
            resValue "string", "openSeaApiKey", "bfbd6061a33e455c8581b594774fecb3"
            resValue "string", "walletConnectV2Key", "8b4f41c60880a3e3ad57d82fddb30568"
            resValue "string", "solscanApiKey", "eyJhbGciOiJIUzI1NiIsInR5cCI6IkpXVCJ9.eyJjcmVhdGVkQXQiOjE2Nzk0NjQyMTQ4NDAsImVtYWlsIjoiaHJ6c3lzdGVtczEwMUBnbWFpbC5jb20iLCJhY3Rpb24iOiJ0b2tlbi1hcGkiLCJpYXQiOjE2Nzk0NjQyMTR9.BRM7J9RbDpHgd2oMAus00XfWOxTJgV2Tn2_chXZOdtk"
            resValue "string", "trongridApiKeys", "33374494-8060-447e-8367-90c5efd4ed95"
            resValue "string", "udnApiKey", "r2phzgatt_zt9-hd_wyvdjrdsrimnxgokm7knyag1malzgcz"
            resValue "string", "oneInchApiKey", "3EttyCzgWb2GLFIRoPIUYM0M4uKAVEcq"
            resValue "string", "blocksDecodedEthereumRpc", "https://api-dev.blocksdecoded.com/v1/ethereum-rpc/mainnet"
            resValue "string", "chainalysisBaseUrl", "https://public.chainalysis.com/api/v1/"
            resValue "string", "chainalysisApiKey", "928bb256db73f1cb93e1b3366a145d9fbe06e28581c8b665b82ad70bbfef1db4"
            resValue "string", "hashDitBaseUrl", "https://api.diting.pro/v2/hashdit/"
            resValue "string", "hashDitApiKey", "KuyxZfvJXFrpAcztshhYqeWaRusxyGRDDhFYkeIw"
        }

        release {
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue "string", "twitterBearerToken", "AAAAAAAAAAAAAAAAAAAAAJgeNwEAAAAA6xVpR6xLKTrxIA3kkSyRA92LDpA%3Da6auybDwcymUyh2BcS6zZwicUdxGtrzJC0qvOSdRwKLeqBGhwB"
            resValue "string", "uniswapGraphUrl", "https://api.thegraph.com/subgraphs/name/uniswap/uniswap-v2"
            resValue "string", "etherscanKey", "IEXTB9RE7MUV2UQ9X238RP146IEJB1J5HS,27S4V3GYJGMCPWQZ2T4SF9355QBQYQ3FI7,YK4KEA3TANM8KZ5J6E2Q1ZIM6YDM8TEABM"
            resValue "string", "bscscanKey", "FQ2HSNNEHVG71U96P1TF3WF9RTF6AF5MRA,G6K8VZDWYSJHTCRURRITFZ2ZWV48GRGTZQ,R396MSJNCKX2YK4EIMP3EWYAW21NSVMXRN,"
            resValue "string", "otherScanKey", "Y855XHV4XKUC9DTRM2ZQG8XAQ96EJV221Q,43DEJEEMA1P81YAU555A1TECRY5FPIWCFH"
            resValue "string", "is_release", "true"
            resValue "string", "guidesUrl", "https://raw.githubusercontent.com/horizontalsystems/blockchain-crypto-guides/v1.2/index.json"
            resValue "string", "eduUrl", "https://raw.githubusercontent.com/horizontalsystems/Unstoppable-Wallet-Website/refs/tags/v1.4/src/edu.json"
            resValue "string", "faqUrl", "https://raw.githubusercontent.com/horizontalsystems/Unstoppable-Wallet-Website/v1.3/src/faq.json"
            resValue "string", "coinsJsonUrl", "https://raw.githubusercontent.com/horizontalsystems/cryptocurrencies/v0.21/coins.json"
            resValue "string", "providerCoinsJsonUrl", "https://raw.githubusercontent.com/horizontalsystems/cryptocurrencies/v0.21/provider.coins.json"
            resValue "string", "marketApiBaseUrl", "https://api.blocksdecoded.com"
            resValue "string", "marketApiKey", "IQf1uAjkthZp1i2pYzkXFDom"
            resValue "string", "openSeaApiKey", "bfbd6061a33e455c8581b594774fecb3"
            resValue "string", "walletConnectV2Key", "0c5ca155c2f165a7d0c88686f2113a72"
            resValue "string", "solscanApiKey", "eyJhbGciOiJIUzI1NiIsInR5cCI6IkpXVCJ9.eyJjcmVhdGVkQXQiOjE2Nzk0NjQxMTQ0ODUsImVtYWlsIjoiaHNkYW84ODg4QGdtYWlsLmNvbSIsImFjdGlvbiI6InRva2VuLWFwaSIsImlhdCI6MTY3OTQ2NDExNH0.91DUjjjYu86f1ZMMJ5cyJxIKTTw_srhI-vNgYaTCPUU"
            resValue "string", "trongridApiKeys", "8f5ae2c8-8012-42a8-b0ca-ffc2741f6a29,578aa64f-a79f-4ee8-86e9-e9860e2d050a,1e92f1fc-41f8-401f-a7f6-5b719b6f1280"
            resValue "string", "udnApiKey", "r2phzgatt_zt9-hd_wyvdjrdsrimnxgokm7knyag1malzgcz"
            resValue "string", "oneInchApiKey", "3EttyCzgWb2GLFIRoPIUYM0M4uKAVEcq"
            resValue "string", "blocksDecodedEthereumRpc", "https://api.blocksdecoded.com/v1/ethereum-rpc/mainnet"
            resValue "string", "chainalysisBaseUrl", "https://public.chainalysis.com/api/v1/"
            resValue "string", "chainalysisApiKey", "928bb256db73f1cb93e1b3366a145d9fbe06e28581c8b665b82ad70bbfef1db4"
            resValue "string", "hashDitBaseUrl", "https://service.hashdit.io/v2/hashdit/"
            resValue "string", "hashDitApiKey", "aGMkgODYiUFtTYrSRcEZsIfPHeASOlGYXClJZNWF"
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    packagingOptions {
        resources {
            pickFirsts += [
                'META-INF/atomicfu.kotlin_module',
                'META-INF/FastDoubleParser-LICENSE',
                'META-INF/FastDoubleParser-NOTICE',
                'META-INF/io.netty.versions.properties'
            ]
            excludes += [
                'META-INF/INDEX.LIST',
                'META-INF/DEPENDENCIES',
                'META-INF/LICENSE.md'
            ]
        }
        jniLibs {
            useLegacyPackaging true
        }
    }

    kotlinOptions {
        jvmTarget = "17"
    }

    composeOptions {
        kotlinCompilerExtensionVersion '1.5.12'
    }
    namespace 'io.horizontalsystems.bankwallet'
    lint {
        disable 'LogNotTimber'
    }

    configurations.all { c ->
        c.resolutionStrategy.dependencySubstitution {
            substitute module('org.bouncycastle:bcprov-jdk15to18:1.68') using module('org.bouncycastle:bcprov-jdk15on:1.65')
            substitute module('com.google.protobuf:protobuf-java:3.6.1') using module('com.google.protobuf:protobuf-javalite:3.21.1')
            substitute module('net.jcip:jcip-annotations:1.0') using module('com.github.stephenc.jcip:jcip-annotations:1.0-1')

            substitute module('com.tinder.scarlet:scarlet:0.1.12') using module('com.github.WalletConnect.Scarlet:scarlet:1.0.0')
            substitute module('com.tinder.scarlet:websocket-okhttp:0.1.12') using module('com.github.WalletConnect.Scarlet:websocket-okhttp:1.0.0')
            substitute module('com.tinder.scarlet:stream-adapter-rxjava2:0.1.12') using module('com.github.WalletConnect.Scarlet:stream-adapter-rxjava2:1.0.0')
            substitute module('com.tinder.scarlet:message-adapter-gson:0.1.12') using module('com.github.WalletConnect.Scarlet:message-adapter-gson:1.0.0')
            substitute module('com.tinder.scarlet:lifecycle-android:0.1.12') using module('com.github.WalletConnect.Scarlet:lifecycle-android:1.0.0')
        }

        resolutionStrategy.eachDependency { details ->
            if (details.requested.group == 'com.squareup.okhttp3') {
                details.useVersion "4.12.0"
            }
        }
    }

}

configurations {
    // Initializes a placeholder for the freeDebugImplementation dependency configuration.
    baseDebugImplementation {}
    baseReleaseImplementation {}
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "androidx.appcompat:appcompat:$appcompat_version"
    implementation "androidx.constraintlayout:constraintlayout:$constraint_version"
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'

    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    // use -ktx for Kotlin
    // alternatively - just LiveData
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    // alternatively - Lifecycles only (no ViewModel or LiveData).
    //     Support library depends on this lightweight import
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.fragment:fragment-ktx:$fragment_ktx_version"
    implementation "androidx.preference:preference-ktx:1.2.1"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"

    //Splash screen
    implementation "androidx.core:core-splashscreen:1.0.1"

    //AppWidgets
    implementation 'androidx.glance:glance-appwidget:1.1.1'

    // Room
    def room_version = '2.8.1'
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    implementation "androidx.room:room-rxjava2:$room_version"
    ksp "androidx.room:room-compiler:$room_version"
    // alternately - if using Java8, use the following instead of compiler
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
    // optional - ReactiveStreams support for LiveData
    implementation "androidx.lifecycle:lifecycle-reactivestreams-ktx:$lifecycle_version"

    implementation 'androidx.recyclerview:recyclerview:1.4.0'

    implementation "com.google.android.material:material:$material_version"

    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    // Because RxAndroid releases are few and far between, it is recommended you also
    // explicitly depend on RxJava's latest version for bug fixes and new features.
    // (see https://github.com/ReactiveX/RxJava/releases for latest 2.x.x version)
    implementation "io.reactivex.rxjava2:rxjava:$rxjava_version"
    implementation 'com.squareup.retrofit2:retrofit:3.0.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:3.0.0'
    implementation 'com.squareup.retrofit2:converter-gson:3.0.0'
    implementation 'com.squareup.retrofit2:converter-scalars:3.0.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:5.1.0'
    implementation 'com.google.code.gson:gson:2.13.2'
    implementation "androidx.biometric:biometric:$biometric_version"

    implementation 'com.atlassian.commonmark:commonmark:0.17.0'
    implementation "io.noties.markwon:core:4.6.2"

    // ViewPager circle indicator
    implementation 'me.relex:circleindicator:2.1.6'

    //Custom tabs, chrome
    implementation "androidx.browser:browser:1.9.0"

    // Navigation component
    implementation "androidx.navigation:navigation-fragment-ktx:$navigation_ktx_version"
    implementation "androidx.navigation:navigation-ui-ktx:$navigation_ktx_version"

    //Compose Navigation
    implementation "androidx.navigation:navigation-compose:$navigation_ktx_version"
    implementation "com.google.accompanist:accompanist-navigation-animation:$accompanist_version"

    api 'com.journeyapps:zxing-android-embedded:4.3.0'

    // QR code generator
    implementation("io.github.alexzhirkevich:qrose:1.0.1")

    // WorkManager Kotlin
    def work_version = "2.10.5"
    implementation "androidx.work:work-runtime-ktx:$work_version"
    // WorkManager RxJava2 support
    implementation "androidx.work:work-rxjava2:$work_version"

    def leakCanary = 'com.squareup.leakcanary:leakcanary-android:2.14'
    debugImplementation leakCanary

    //Logging
    implementation "com.jakewharton.timber:timber:5.0.1"

    // Wallet kits
    implementation 'com.github.horizontalsystems:monero-kit-android:303909b'
    implementation 'com.github.horizontalsystems:stellar-kit-android:519533b'
    implementation 'com.github.horizontalsystems:ton-kit-android:dcddd90'
    implementation 'com.github.horizontalsystems:bitcoin-kit-android:2bfdc13'
    implementation 'com.github.horizontalsystems:ethereum-kit-android:fa7b72e'
    implementation 'com.github.horizontalsystems:blockchain-fee-rate-kit-android:7d01e63'
    implementation 'com.github.horizontalsystems:market-kit-android:8bd85cc'
    implementation 'com.github.horizontalsystems:solana-kit-android:eae6858'
    implementation 'com.github.horizontalsystems:tron-kit-android:16ae04a'
    // Zcash SDK
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.5'
    implementation "cash.z.ecc.android:zcash-android-sdk:2.4.0"
    implementation("io.github.binance:binance-connector-java:3.4.1") {
        exclude group: "org.bouncycastle", module: "bcprov-jdk18on"
    }


    // Exclude old version from wherever it's coming
    configurations.configureEach {
        exclude group: "org.bouncycastle", module: "bcprov-jdk18on"
        exclude group: "org.bouncycastle", module: "bcutil-jdk18on"
    }

    // WalletConnect V2
    implementation(platform("com.walletconnect:android-bom:1.15.0"))
    implementation 'com.walletconnect:web3wallet'
    implementation 'com.walletconnect:android-core'

    // Unstoppable Domains
    implementation 'com.github.unstoppabledomains:resolution-java:v7.1.0'

    // Ethereum Name Service
    implementation "org.web3j:core:4.9.0"

    implementation 'com.twitter.twittertext:twitter-text:3.1.0'

    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.10.2'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-rx2:1.10.2'

    // UI modules

    implementation project(':core')
    implementation project(':components:icons')
    implementation project(':components:chartview')

    implementation project(':subscriptions-core')

    baseDebugImplementation project(':subscriptions-dev')
    baseReleaseImplementation project(':subscriptions-google-play')

    fdroidImplementation project(':subscriptions-fdroid')
    fdroidCiImplementation project(':subscriptions-fdroid')
    ciImplementation project(':subscriptions-dev')

    // Integration with activities
    implementation 'androidx.activity:activity-compose:1.11.0'
    // Compose Material Design
    implementation "androidx.compose.material:material:$compose_version"
    // Animations
    implementation "androidx.compose.animation:animation:$compose_version"
    // Tooling support (Previews, etc.)
    implementation "androidx.compose.ui:ui-tooling:$compose_version"
    // Integration with ViewModels
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:$lifecycle_version"
    // Lifecycle utilities for Compose
    implementation "androidx.lifecycle:lifecycle-runtime-compose:$lifecycle_version"

    implementation "androidx.compose.runtime:runtime-livedata:$compose_version"

    //Material 3
    implementation 'com.google.android.material:material:1.13.0'
    implementation "androidx.compose.material3:material3:1.4.0"

    def coil_version = "2.7.0"
    implementation "io.coil-kt:coil-compose:$coil_version"
    implementation "io.coil-kt:coil-svg:$coil_version"
    implementation("io.coil-kt:coil-gif:$coil_version")

    // When using a AppCompat theme
    implementation "com.google.accompanist:accompanist-appcompat-theme:$accompanist_version"
    implementation "com.google.accompanist:accompanist-flowlayout:$accompanist_version"
    implementation "com.google.accompanist:accompanist-permissions:0.37.3"

    // UI Tests
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"

    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    // optional - Test helpers for LiveData
    testImplementation "junit:junit:$junit_version"
    testImplementation "androidx.arch.core:core-testing:2.2.0"
    testImplementation "org.mockito:mockito-core:3.3.3"
    testImplementation 'com.nhaarman:mockito-kotlin-kt1.1:1.6.0'
    testImplementation 'org.powermock:powermock-api-mockito2:2.0.7'
    testImplementation 'org.powermock:powermock-module-junit4:2.0.7'

    // Spek
    testImplementation "org.spekframework.spek2:spek-dsl-jvm:2.0.9"
    testRuntimeOnly "org.spekframework.spek2:spek-runner-junit5:2.0.9"
    testRuntimeOnly "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    //Android Tor
    implementation 'info.guardianproject:tor-android:0.4.8.17.2'
    implementation 'info.guardianproject:jtorctl:0.4.5.7'

    api 'com.jaredrummler:android-shell:1.0.0'
    api 'com.offbynull.portmapper:portmapper:2.0.6'
}

configurations.all {
    resolutionStrategy {
        cacheChangingModulesFor 0, 'seconds'

        // Force Ktor version for TonKit
        force "io.ktor:ktor-utils:2.3.7"
        force "io.ktor:ktor-io:2.3.7"
        force "io.ktor:ktor-client-core:2.3.7"
    }
}
