apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

Properties buildProperties = new Properties()
buildProperties.load(new BufferedReader(new InputStreamReader(new FileInputStream("app/confs/wallet.properties"), "UTF8")))

androidExtensions {
    experimental = true
}

def computeVersionCode() {
    ext.buildNumber = System.getenv("BUILD_NUMBER") ?: 1
    return "$buildNumber".toInteger()
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId buildProperties.get("application_id")
        minSdkVersion 23
        targetSdkVersion 28
        versionCode computeVersionCode()
        versionName "0.1.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        resValue "string", "localizations", buildProperties.get("localizations")

        kapt {
            arguments {
                arg("room.schemaLocation", "$projectDir/schemas".toString())
            }
        }
    }
    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
        }
        release {
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "network"
    productFlavors {
        devTestnet {
            buildConfigField "boolean", "testMode", "true"
            applicationIdSuffix ".dev.testnet"
        }
        devMainnet {
            buildConfigField "boolean", "testMode", "false"
            applicationIdSuffix ".dev"
        }
        productionMainnet {
            buildConfigField "boolean", "testMode", "false"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    def lifecycle_version = "1.1.1"
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'

    // ViewModel and LiveData
    implementation "android.arch.lifecycle:extensions:$lifecycle_version"
    // alternatively - just ViewModel
    implementation "android.arch.lifecycle:viewmodel:$lifecycle_version"
    // use -ktx for Kotlin
    // alternatively - just LiveData
    implementation "android.arch.lifecycle:livedata:$lifecycle_version"
    // alternatively - Lifecycles only (no ViewModel or LiveData).
    //     Support library depends on this lightweight import
    implementation "android.arch.lifecycle:runtime:$lifecycle_version"
    kapt "android.arch.lifecycle:compiler:$lifecycle_version"
    // Room
    def room_version = "1.1.1"
    implementation "android.arch.persistence.room:runtime:$room_version"
    implementation "android.arch.persistence.room:rxjava2:$room_version"
    kapt "android.arch.persistence.room:compiler:$room_version"
    // alternately - if using Java8, use the following instead of compiler
    implementation "android.arch.lifecycle:common-java8:$lifecycle_version"
    // optional - ReactiveStreams support for LiveData
    implementation "android.arch.lifecycle:reactivestreams:$lifecycle_version"

    implementation 'com.android.support:recyclerview-v7:28.0.0'

    implementation 'com.aurelhubert:ahbottomnavigation:2.2.0'
    implementation 'com.android.support:design:28.0.0'

    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    // Because RxAndroid releases are few and far between, it is recommended you also
    // explicitly depend on RxJava's latest version for bug fixes and new features.
    // (see https://github.com/ReactiveX/RxJava/releases for latest 2.x.x version)
    implementation 'io.reactivex.rxjava2:rxjava:2.1.14'
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.9.1'
    implementation 'com.google.code.gson:gson:2.8.5'

    api 'com.aurelhubert:ahbottomnavigation:2.2.0'
    api "org.jetbrains.anko:anko-commons:0.10.5"

    api 'com.journeyapps:zxing-android-embedded:3.6.0'

    // For images
    implementation 'com.facebook.fresco:fresco:1.12.0'

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.6.2'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.6.2'

    // BitcoinKit
    implementation 'com.github.horizontalsystems:bitcoin-kit-android:15b5ff5'

    // EthereumKit
    implementation 'com.github.horizontalsystems:ethereum-kit-android:ce04781'

    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    // optional - Test helpers for LiveData
    testImplementation 'junit:junit:4.12'
    testImplementation "android.arch.core:core-testing:$lifecycle_version"
    testImplementation 'org.mockito:mockito-core'
    testImplementation 'com.nhaarman:mockito-kotlin-kt1.1:1.6.0'
    testImplementation 'org.powermock:powermock-api-mockito2:2.0.0-beta.5'
    testImplementation 'org.powermock:powermock-module-junit4:2.0.0-beta.5'
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}
